buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.ajoberstar:grgit:2.1.0'
        classpath 'org.ajoberstar.reckon:reckon-gradle:0.3.0'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'java-library'
apply plugin: "org.ajoberstar.grgit"
apply plugin: "org.ajoberstar.reckon"
apply plugin: 'com.bmuschko.nexus'

group = "cl.daplay"
description = "Rut implementation in Java"

reckon {
    normal = scopeFromProp()
    preRelease = snapshotFromProp()
}

repositories {
    mavenCentral()
    jcenter()
}

targetCompatibility = 1.5
sourceCompatibility = 1.5

dependencies {
    testImplementation 'junit:junit:4.12'
}

task readmeUpdate {
    def versionPattern = /\d\.\d\.\d/
    description = 'Updates version in README.md'

    def execute = !version.toString().contains("SNAPSHOT")
    if (!execute) {
        println "README.md won't be updated, as $version is an SNAPSHOT version"
        return;
    }

    if (!(version.toString() ==~ versionPattern)) {
        println "README.md won't be updated, as $version is not a release version"
        return;
    }

    // handle to README.md
    def readme = new File(project.projectDir, "README.md")

    // replace text in README.md
    doLast {
        def text = readme.text
        readme.text = text.replaceAll versionPattern, version.toString()
    }

    // add, commit and push change
    doLast {
        grgit.add(patterns: ['README.md'])
        grgit.commit(message: "chore(README.md) releases version: '${version.toString()}'.")
        grgit.push
    }
}

task release() {
    def execute = !version.toString().contains("SNAPSHOT")
    if (!execute) {
        def m = "Can't release version: $version, please use -Preckon.scope=<major|minor|patch> and -Preckon.snapshot=false to release"
        throw new GradleException(m)
    }
}

release.dependsOn build
release.dependsOn readmeUpdate
release.dependsOn reckonTagCreate
release.dependsOn reckonTagPush
release.finalizedBy uploadArchives

modifyPom {
    project {

        name project.name
        description project.description
        url 'https://github.com/daplay/jrut'

        scm {
            url 'https://github.com/daplay/jrut'
            connection 'scm:https://github.com/daplay/jrut.git'
            developerConnection 'scm:git://github.com/daplay/jrut.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'daplay'
                name 'Daniel Meneses'
                email 'daplay@gmail.com'
            }
        }
    }
}

